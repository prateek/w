---
source: tests/integration_tests/init.rs
info:
  program: wt
  args:
    - config
    - shell
    - init
    - zsh
  env:
    CLICOLOR_FORCE: "1"
    COLUMNS: "150"
    GIT_AUTHOR_DATE: "2025-01-01T00:00:00Z"
    GIT_COMMITTER_DATE: "2025-01-01T00:00:00Z"
    GIT_CONFIG_GLOBAL: "[TEST_GIT_CONFIG]"
    GIT_CONFIG_SYSTEM: /dev/null
    GIT_EDITOR: ""
    LANG: C
    LC_ALL: C
    SOURCE_DATE_EPOCH: "1761609600"
    WORKTRUNK_CONFIG_PATH: "[TEST_CONFIG]"
---
success: true
exit_code: 0
----- stdout -----
# worktrunk shell integration for zsh

# Only initialize if wt is available (in PATH or via WORKTRUNK_BIN)
if command -v wt >/dev/null 2>&1 || [[ -n "${WORKTRUNK_BIN:-}" ]]; then
    # Use WORKTRUNK_BIN if set, otherwise resolve binary path
    # Must resolve BEFORE defining shell function, so lazy completion can call binary directly
    # This allows testing development builds: export WORKTRUNK_BIN=./target/debug/wt
    _WORKTRUNK_CMD="${WORKTRUNK_BIN:-$(command -v wt)}"

# Shared directive parser for POSIX shells (bash, zsh, oil).
# Streams worktrunk's NUL-delimited output in real-time via FIFO while keeping
# stderr attached to the TTY for child processes (colors, progress bars).
#
# Note: Named without leading underscore to avoid being filtered by shell
# snapshot systems (e.g., Claude Code) that exclude private functions.
# Note: Uses ${_WORKTRUNK_CMD:-wt} fallback because shell snapshot
# systems may capture functions but not environment variables.
wt_exec() {
    # Disable job control notifications in zsh (prevents "[1] 12345" / "[1] + done" messages)
    # For bash, this is handled at the backgrounding site using fd redirection
    if [[ -n "${ZSH_VERSION:-}" ]]; then
        setopt LOCAL_OPTIONS NO_MONITOR
    fi

    local exec_cmd="" chunk="" exit_code=0 tmp_dir="" fifo_path="" runner_pid=""

    # Cleanup handler for signals and normal exit
    _wt_cleanup() {
        # Kill background process if still running
        if [[ -n "$runner_pid" ]] && kill -0 "$runner_pid" 2>/dev/null; then
            kill "$runner_pid" 2>/dev/null || true
        fi
        # Remove temp files
        /bin/rm -f "$fifo_path" 2>/dev/null || true
        /bin/rmdir "$tmp_dir" 2>/dev/null || true
    }

    # On SIGINT: cleanup and exit immediately with 130
    trap '_wt_cleanup; return 130' INT

    # Create temp directory with FIFO for streaming output
    tmp_dir=$(mktemp -d "${TMPDIR:-/tmp}/wt.XXXXXX") || {
        echo "Failed to create temp directory for worktrunk shim" >&2
        return 1
    }
    fifo_path="$tmp_dir/stdout.fifo"

    if ! mkfifo "$fifo_path"; then
        echo "Failed to create FIFO for worktrunk shim" >&2
        /bin/rm -rf "$tmp_dir"
        return 1
    fi

    # Run worktrunk in background, piping stdout to FIFO
    # (stderr stays attached to TTY for child process colors/progress)
    # For bash, redirect stderr for the { & } to suppress job notifications,
    # but keep command's stderr on the original fd
    if [[ -n "${BASH_VERSION:-}" ]]; then
        exec 9>&2
        { command "${_WORKTRUNK_CMD:-wt}" "$@" >"$fifo_path" 2>&9 & } 2>/dev/null
        runner_pid=$!
        exec 9>&-
    else
        command "${_WORKTRUNK_CMD:-wt}" "$@" >"$fifo_path" &
        runner_pid=$!
    fi

    # Parse directives as they stream in
    while IFS= read -r -d '' chunk || [[ -n "$chunk" ]]; do
        if [[ "$chunk" == __WORKTRUNK_CD__* ]]; then
            # Directory change directive
            local path="${chunk#__WORKTRUNK_CD__}"
            \cd "$path"
        elif [[ "$chunk" == __WORKTRUNK_EXEC__* ]]; then
            # Command execution directive (deferred until after worktrunk exits)
            exec_cmd="${chunk#__WORKTRUNK_EXEC__}"
        else
            # Regular output - print to stdout
            [[ -n "$chunk" ]] && printf '%s\n' "$chunk"
        fi
    done <"$fifo_path"

    # Wait for worktrunk to complete and capture its exit code
    wait "$runner_pid" >/dev/null 2>&1 || exit_code=$?

    # Cleanup
    trap - INT
    _wt_cleanup

    # Execute deferred command if specified (its exit code takes precedence)
    if [[ -n "$exec_cmd" ]]; then
        eval "$exec_cmd"
        exit_code=$?
    fi

    return "${exit_code:-0}"
}

    # Override wt command to add --internal flag
    wt() {
        # Initialize _WORKTRUNK_CMD if not set (e.g., after shell snapshot restore)
        if [[ -z "$_WORKTRUNK_CMD" ]]; then
            _WORKTRUNK_CMD="${WORKTRUNK_BIN:-$(command -v wt)}"
        fi

        local use_source=false
        local -a args
        local saved_cmd="$_WORKTRUNK_CMD"

        # Check for --source flag and strip it
        for arg in "$@"; do
            if [[ "$arg" == "--source" ]]; then
                use_source=true
            else
                args+=("$arg")
            fi
        done

        # If --source was specified, build and use local debug binary
        if [[ "$use_source" == true ]]; then
            if ! cargo build --quiet; then
                _WORKTRUNK_CMD="$saved_cmd"
                return 1
            fi
            _WORKTRUNK_CMD="./target/debug/wt"
        fi

        # Force colors if stderr is a TTY (directive mode outputs to stderr)
        # Respects NO_COLOR and explicit CLICOLOR_FORCE
        if [[ -z "${NO_COLOR:-}" && -z "${CLICOLOR_FORCE:-}" ]]; then
            if [[ -t 2 ]]; then export CLICOLOR_FORCE=1; fi
        fi

        # Always use --internal mode for directive support
        wt_exec --internal "${args[@]}"

        # Restore original command
        local result=$?
        _WORKTRUNK_CMD="$saved_cmd"
        return $result
    }

    # Lazy completion loader - loads real completions on first tab-press
    # This avoids ~11ms binary invocation at shell startup
    _wt_lazy_complete() {
        # Only try to install completions once
        if [[ -z "${_WT_COMPLETION_LOADED:-}" ]]; then
            typeset -g _WT_COMPLETION_LOADED=1
            local completion_script
            if completion_script=$(COMPLETE=zsh "${_WORKTRUNK_CMD:-wt}" 2>/dev/null); then
                eval "$completion_script"
            else
                # Failed to load - unregister to prevent future silent failures
                compdef -d wt 2>/dev/null
                return 1
            fi
        fi

        # Delegate to real completion function if it was installed
        if (( $+functions[_clap_dynamic_completer_wt] )); then
            _clap_dynamic_completer_wt
        fi
    }

    # Register stub, but only if compdef is available (compinit was run)
    if (( $+functions[compdef] )); then
        compdef _wt_lazy_complete wt
    fi
fi

----- stderr -----
